R1 = 0.05;  R2 = 0.04;
k1 = 1e6;   k2 = 1e6;
t = 0:1:400;

function dXdt = model(t, X, R1, R2, k1, k2, alpha, beta)
    x = X(1); y = X(2);
    dxdt = R1 * x * (k1 - x - alpha * y) / k1;
    dydt = R2 * y * (k2 - y - beta * x) / k2;
    dXdt = [dxdt; dydt];
endfunction

// Параметры сценариев
alpha_vals = [0.5, 2.0, 0.5, 2.0];
beta_vals  = [2.0, 0.5, 0.5, 2.0];

titles = [
    "Сценарий 1: x вытесняет y";
    "Сценарий 2: y вытесняет x";
    "Сценарий 3: Сосуществование";
    "Сценарий 4: Исход зависит от старта"
];

// Одно начальное условие на сценарий
IC = [
    5e5, 5e5;     // С1
    5e5, 5e5;     // С2
    5e5, 5e5;     // С3
    5.5e5, 4.5e5  // С4: x чуть больше
];

for s = 1:4
    alpha = alpha_vals(s);
    beta  = beta_vals(s);
    x0 = IC(s, 1);
    y0 = IC(s, 2);
    X0 = [x0; y0];
    
    function dXdt = temp(t, X)
        dXdt = model(t, X, R1, R2, k1, k2, alpha, beta);
    endfunction
    
    X = ode(X0, 0, t, temp);
    x = X(1,:);
    y = X(2,:);
    
    // временная динамика
    scf(2*s - 1); clf();
    plot(t, x, 'b-', 'LineWidth', 2);
    plot(t, y, 'r--', 'LineWidth', 2);
    xlabel('Время');
    ylabel('Численность');
    title(titles(s) + " — динамика во времени");
    xgrid();
    legend(['Популяция x', 'Популяция y'], 'in_upper_right');
    
    //  фазовый портрет
    scf(2*s); clf();
    plot(x, y, 'g-', 'LineWidth', 2);
    xlabel('Численность x (популяция 1)');
    ylabel('Численность y (популяция 2)');
    title(titles(s) + " — фазовый портрет");
    xgrid();
    
    // Отметим начальную точку
    plot(x(1), y(1), 'bo', 'MarkerSize', 6, 'MarkerFaceColor', 'b');
    // Отметим конечную точку
    plot(x($), y($), 'ro', 'MarkerSize', 6, 'MarkerFaceColor', 'r');
    legend(['Траектория', 'Начало', 'Конец'], 'in_upper_right');
end
